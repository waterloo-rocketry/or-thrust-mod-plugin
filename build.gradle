plugins {
    id 'java'
}

group 'com.waterloorocketry'
version '1.0-SNAPSHOT'

repositories {
    flatDir {
        dirs './'
    }
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation name: 'OpenRocket', version: '24.12.beta.01', group: 'info.openrocket'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.register('importToOR', Copy) {
    from jar
    into {
        def os = System.getProperty('os.name').toLowerCase()
        if (os.contains('win')) {
            return System.getenv('APPDATA') + "/OpenRocket/plugins"
        } else if (os.contains('mac')) {
            return System.getProperty('user.home') + "/Library/Application Support/OpenRocket/plugins"
        } else if (os.contains('nix') || os.contains('nux') || os.contains('aix')) {
            return System.getProperty('user.home') + "/.openrocket/plugins"
        } else {
            throw new GradleException("Unsupported operating system: $os")
        }
    }

    dependsOn build
}

tasks.register('run', JavaExec) {
    dependsOn jar
    mainClass = 'info.openrocket.swing.startup.OpenRocket'
    classpath = files('./OpenRocket-24.12.beta.01.jar', tasks.jar)
    systemProperty 'java.library.path', 'build/natives/lib'
}